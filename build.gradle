plugins {
    id "com.github.samueltbrown.cucumber" version "0.9"
}
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: "com.github.samueltbrown.cucumber"
apply plugin: 'jetty'

sourceSets {
    selenium
}

sourceCompatibility = 1.8
version = '1.0'

group = "edu.rosehulman.rosebuild"

repositories {
	mavenCentral()
 }

jacoco {
    toolVersion = "0.7.5.+"
}

jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
    
	reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

dependencies {
    testCompile 'junit:junit:4.+', 'info.cukes:cucumber-java:1.2.2', 'org.seleniumhq.selenium:selenium-java:3.4.0'
    cucumberCompile 'junit:junit:4.+', 'info.cukes:cucumber-java:1.2.2', 'org.seleniumhq.selenium:selenium-java:3.4.0'
    seleniumCompile 'junit:junit:4.+', 'org.seleniumhq.selenium:selenium-java:3.4.0'
}

task jettyDaemon(type: org.gradle.api.plugins.jetty.JettyRun) {
    daemon = true
}

task selenium(type: Test, dependsOn: jettyDaemon) {
    testClassesDir = sourceSets.selenium.output.classesDir
    classpath = sourceSets.selenium.runtimeClasspath
}


cucumber {
	formats = ['pretty','junit:build/cucumber.xml']
	monochrome = true
}


test {
    jacoco {
        append = false
    }
}

//test.finalizedBy(jacocoTestReport)

task codeCoverageInfo (dependsOn: jacocoTestReport) {
    ext.srcFile = file("${reportsDir}/jacoco/test/jacocoTestReport.xml")
    doLast {
        println "Transforming source file."
		
		def parser = new XmlParser()
		parser.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false); 
		parser.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false); 
		parser.setFeature("http://xml.org/sax/features/namespaces", false)
		 		 
        def reportNode = parser.parse(srcFile)
        reportNode.children().each {
        	if(it.name() == 'counter') {
        		String type = it.@type
        		float missed = it.@missed as float
        		float covered = it.@covered as float
        		float percentage = covered / (missed + covered) * 100
        		println "Code Coverage[$type]: $percentage%"
        	}        	
        }
    }
}
jacocoTestReport.finalizedBy(codeCoverageInfo)


jar {
    manifest {
        attributes 'Implementation-Title': 'Rose Hulman Build System Library',
                   'Implementation-Version': '${version}'
    }
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}
